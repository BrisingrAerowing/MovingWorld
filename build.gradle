buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'idea'

def branch
if (System.env.BRANCH_NAME) {
    // Jenkins support
    branch = System.env.BRANCH_NAME
    branch = branch.substring(branch.lastIndexOf('/') + 1)
} else {
    branch = 'git rev-parse --abbrev-ref HEAD'.execute().in.text.trim()
}
def commits = 'git rev-list --count HEAD'.execute().in.text.trim()
def dirty = !'git diff-index HEAD'.execute().in.text.trim().isEmpty()

version = branch + '-' + modVersion + '.' + commits + (dirty ? '-dirty' : '')
group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = project.forgeVersion
    runDir = "run"

    def replacementVersion = 'MOD_VERSION = \"' + project.version + '\"'

    replace("MOD_VERSION = \"@MOVINGWORLDVER@\"",
            replacementVersion)

    mappings = project.mcpVersion
}

mixin {
    add sourceSets.main, "mixins.movingworld.refmap.json"
}

repositories {
    maven {
        name 'spongepowered'
        url 'https://repo.spongepowered.org/maven/'
    }
    maven {
        url = 'http://repo.elytradev.com/'
    }
}

dependencies {
    compile('org.spongepowered:mixin:0.6.9-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
    }

    compile 'com.elytradev:concrete:0.2.2:all'
    shadow 'com.elytradev:concrete:0.2.2:all'
	
	compile 'cglib:cglib:3.2.5'
    compile 'org.objenesis:objenesis:2.5.1'
}

shadowJar {
    exclude 'dummyThing'

	relocate 'com.elytradev.concrete', "com.elytradev.movingworld.repackage.com.elytradev.concrete"
    configurations = [project.configurations.shadow]

    dependencies {
        include(dependency('org.spongepowered:mixin'))
    }

    classifier = 'full'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)

jar {
    manifest {
        attributes(
                'MixinConfigs': 'mixins.movingworld.json',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': '0',
                'FMLCorePlugin': 'com.elytradev.movingworld.common.asm.coremod.MovingWorldCoreMod',
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true',
                'FMLAT': 'MovingWorld_at.cfg'
        )
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

idea {
    workspace {
        if (iws != null) {
            iws.withXml { xmlFile ->
                //Please don't kill me for my shitty groovy usage ;-;
                def runManager = xmlFile.asNode().component.find { it.@name == 'RunManager' }

                def minecraftClientDefaults = runManager.configuration.find {
                    it.@name == 'Minecraft Client' && it.@type == 'Application'
                }

                def minecraftServerDefaults = runManager.configuration.find {
                    it.@name == 'Minecraft Server' && it.@type == 'Application'
                }

                def minecraftClientVMArgs = '-Xincgc -Xmx1024M -Xms1024M -Dfml.coreMods.load=com.elytradev.movingworld.common.asm.coremod.MovingWorldCoreMod -Dmixin.debug=true'
                minecraftClientDefaults.option.find { it.@name == 'VM_PARAMETERS' }.replaceNode {
                    option(name: 'VM_PARAMETERS', value: minecraftClientVMArgs)
                }

                def minecraftServerVMArgs = '-Xincgc -Dfml.ignoreInvalidMinecraftCertificates=true -Dfml.coreMods.load=com.elytradev.movingworld.common.asm.coremod.MovingWorldCoreMod -Dmixin.debug=true'
                minecraftServerDefaults.option.find { it.@name == 'VM_PARAMETERS' }.replaceNode {
                    option(name: 'VM_PARAMETERS', value: minecraftServerVMArgs)
                }
            }
        }
    }
}
