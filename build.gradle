buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

repositories {
    maven {
        url = 'http://unascribed.com/maven/releases'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "1.0"
group = "elytra.movingworld"
archivesBaseName = "movingworld-REDUX"

minecraft {
    version = "1.9-12.16.0.1854-1.9"
    runDir = "run"
    mappings = "snapshot_20160401"

    replace '@MOVINGWORLDVER@', project.version
}

jar {
    classifier = 'slim'

    manifest {
        attributes 'FMLAT': 'MovingWorld_at.cfg'
    }
}

shadowJar {
    classifier = ''

    exclude 'dummyThing'
    relocate 'com.unascribed.lambdanetwork', "elytra.movingworld.com.unascribed.lambdanetwork"
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

artifacts {
    archives shadowJar
}

dependencies {
    compile 'com.unascribed:lambdanetwork:0.0.3'
    shadow 'com.unascribed:lambdanetwork:0.0.3'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
